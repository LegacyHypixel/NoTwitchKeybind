plugins {
	id 'java'
	id 'xyz.wagyourtail.unimined' version '1.0.5'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven {
		name = "wagyourtail releases"
		url = "https://maven.wagyourtail.xyz/releases"
	}
	maven {
		name = "sponge"
		url = "https://repo.spongepowered.org/maven"
	}
	mavenCentral()
}

sourceSets {
	fabric {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
	forge {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
}

tasks.register("fabricJar", Jar) {
	from sourceSets.fabric.output, sourceSets.main.output

	archiveClassifier = "fabric"
}

tasks.register("forgeJar", Jar) {
	from sourceSets.forge.output, sourceSets.main.output

	archiveClassifier = "forge"
}

unimined.minecraft {
	version project.minecraft_version

	mappings {
		searge()
		mcp("stable", "22-1.8.9")
	}

	defaultRemapJar = false
}

unimined.minecraft(sourceSets.fabric) {
	version project.minecraft_version

	mappings {
		searge()
		mcp("stable", "22-1.8.9")
	}

	legacyFabric {
		loader project.fabric_version
	}
}

unimined.minecraft(sourceSets.forge) {
	version project.minecraft_version

	mappings {
		searge()
		mcp("stable", "22-1.8.9")
	}

	minecraftForge {
		loader project.forge_version
		mixinConfig "notwitchkeybind.mixins.json"
	}
}

configurations {
	mainImplementation
	implementation {
		extendsFrom forgeImplementation
		extendsFrom fabricImplementation
	}
}

sourceSets {
	main {
		compileClasspath += configurations.mainImplementation
		runtimeClasspath += configurations.mainImplementation
	}
}

dependencies {
	mainImplementation "org.spongepowered:mixin:0.8.5-SNAPSHOT"
}

processFabricResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

processForgeResources {
	inputs.property "version", project.version

	filesMatching("META-INF/mods.toml") {
		expand "version": project.version
	}

	filesMatching("mcmod.info") {
		expand "version": project.version
	}
}

// Ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	if (JavaVersion.current().isJava9Compatible()) it.options.release = 8

	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
}

jar {
	enabled = false
}
